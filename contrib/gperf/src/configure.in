dnl autoconf configuration for gperf/src

dnl Copyright (C) 1998, 2000 Free Software Foundation, Inc.
dnl written by Douglas C. Schmidt (schmidt@ics.uci.edu)
dnl
dnl This file is part of GNU GPERF.
dnl
dnl GNU GPERF is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 1, or (at your option)
dnl any later version.
dnl
dnl GNU GPERF is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with GNU GPERF; see the file COPYING.  If not, write to the
dnl Free Software Foundation, 59 Temple Place - Suite 330, Boston,
dnl MA 02111-1307, USA.

AC_INIT(gen-perf.cc)
AC_CONFIG_HEADER(config.h)
AC_PROG_MAKE_SET
dnl
dnl           checks for programs
dnl
AC_PROG_CC
                      dnl sets variable CC
AC_PROG_CPP
                      dnl sets variable CPP
AC_PROG_CXX
                      dnl sets variable CXX
AC_PROG_CXXCPP
                      dnl sets variable CXXCPP
CL_PROG_INSTALL
                      dnl sets variables INSTALL, INSTALL_DATA, INSTALL_PROGRAM
dnl
dnl           checks for compiler characteristics
dnl
AC_MSG_CHECKING([for working throw()])
AC_CACHE_VAL(gp_cv_cxx_throw_decl,[
AC_LANG_SAVE()
AC_LANG_CPLUSPLUS()
AC_TRY_COMPILE([#include <stdlib.h>
void operator delete (void* ptr) throw() {}], [],
gp_cv_cxx_throw_decl=yes, gp_cv_cxx_throw_decl=no)
AC_LANG_RESTORE()
])
AC_MSG_RESULT([$]gp_cv_cxx_throw_decl)
if test [$]gp_cv_cxx_throw_decl = yes; then
  AC_DEFINE(HAVE_THROW_DECL)
fi
dnl
dnl           checks for functions and declarations
dnl
AC_CHECK_HEADERS(unistd.h sys/time.h sys/resource.h)
                      dnl DEFs HAVE_UNISTD_H, HAVE_SYS_TIME_H, HAVE_SYS_RESOURCE_H
if test $ac_cv_header_sys_resource_h = yes; then
AC_CHECK_FUNCS(getrlimit)
                      dnl DEFS HAVE_GETRLIMIT
if test $ac_cv_func_getrlimit = yes; then
AC_CHECK_FUNCS(setrlimit)
                      dnl DEFS HAVE_SETRLIMIT
fi
fi
dnl
dnl           That's it.
dnl
AC_OUTPUT(Makefile)
